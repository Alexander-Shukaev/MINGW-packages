# Maintainer: Haroogan <Haroogan@gmail.com>
# Contributer: Martell Malone <martellmalone@gmail.com>

_realname='conemu'
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r3194.78fc212
pkgrel=1
pkgdesc="Customizable Windows console emulator with tabs (mingw-w64)"
url="http://code.google.com/p/${_realname}-maximus5/"
license=('BSD New')
arch=('any')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "git")
# CAUTION:
# Don't remove `!strip' option!  Otherwise, 64-bit version of ConEmu will signal
# errors at startup.
options=('!strip'
         'staticlibs')
source=("${_realname}"::"git+http://github.com/Haroogan/ConEmu.git#branch=msys2"
        'ConEmu.xml')
sha256sums=('SKIP'
            '84c201cf676aae844a5c00b20ce8912f004562422a3e340fe01c786ab666062c')

pkgver() {
  cd "${_realname}"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_realname}/src"

  # CAUTION:
  # Don't remove `-static*' switches from `USERLIBSFIRST'!  Otherwise, when
  # running `cmd.exe' (Command Prompt) in ConEmu, it cannot find
  # `libstdc++-6.dll' if it is not in the current `PATH' environment variable.
  # CAUTION:
  # Don't remove `-j1' switch!  Otherwise, you might get unexpected problems
  # with build.
  make \
    -j1 \
    -f makefile_all_gcc \
             WIDE="1" \
          USERCPP="-std=c++11 -pipe -O3 -fomit-frame-pointer -funroll-loops -fpermissive" \
    USERLIBSFIRST="-static -static-libgcc -static-libstdc++"
}

package_mingw-w64-i686-conemu-git() {
  optdepends=('mingw-w64-x86_64-conemu-git')

  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin/ConEmu"

  cp "ConEmu.xml" "${pkgdir}${MINGW_PREFIX}/bin/ConEmu"

  cd "${_realname}/Release"

  cp "ConEmu.exe" "${pkgdir}${MINGW_PREFIX}/bin"

  cd "ConEmu"

  cp "ConEmuC.exe"  "${pkgdir}${MINGW_PREFIX}/bin/ConEmu/ConEmuC.exe"
  cp "ConEmuCD.dll" "${pkgdir}${MINGW_PREFIX}/bin/ConEmu/ConEmuCD.dll"
  cp "ConEmuHk.dll" "${pkgdir}${MINGW_PREFIX}/bin/ConEmu/ConEmuHk.dll"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/../mingw64/bin/ConEmu"

  cp "ConEmuC.exe"  "${pkgdir}${MINGW_PREFIX}/../mingw64/bin/ConEmu/ConEmuC.exe"
  cp "ConEmuCD.dll" "${pkgdir}${MINGW_PREFIX}/../mingw64/bin/ConEmu/ConEmuCD.dll"
  cp "ConEmuHk.dll" "${pkgdir}${MINGW_PREFIX}/../mingw64/bin/ConEmu/ConEmuHk.dll"
}

package_mingw-w64-x86_64-conemu-git() {
  optdepends=('mingw-w64-i686-conemu-git')

  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin/ConEmu"

  cp "ConEmu.xml" "${pkgdir}${MINGW_PREFIX}/bin/ConEmu"

  cd "${_realname}/Release"

  cp "ConEmu.exe" "${pkgdir}${MINGW_PREFIX}/bin"

  cd "ConEmu"

  cp "ConEmuC.exe"  "${pkgdir}${MINGW_PREFIX}/bin/ConEmu/ConEmuC64.exe"
  cp "ConEmuCD.dll" "${pkgdir}${MINGW_PREFIX}/bin/ConEmu/ConEmuCD64.dll"
  cp "ConEmuHk.dll" "${pkgdir}${MINGW_PREFIX}/bin/ConEmu/ConEmuHk64.dll"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/../mingw32/bin/ConEmu"

  cp "ConEmuC.exe"  "${pkgdir}${MINGW_PREFIX}/../mingw32/bin/ConEmu/ConEmuC64.exe"
  cp "ConEmuCD.dll" "${pkgdir}${MINGW_PREFIX}/../mingw32/bin/ConEmu/ConEmuCD64.dll"
  cp "ConEmuHk.dll" "${pkgdir}${MINGW_PREFIX}/../mingw32/bin/ConEmu/ConEmuHk64.dll"
}
